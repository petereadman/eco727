## Exercise 4: Factor Variables, CPS-ORG/CPI Data, 1982–2024

Peter Readman

---

### Plan    

---

- Use the Merged CPS-ORG/CPI Data from Exercise 3
- Estimate a Simple log-Wage Regression
- Add Factor Variables
- Estimate Year-Specific Grade Coefficients
- Merge the Grade Coefficients with the 90-10 Differential (in logs)
- Plot the Relationship Between the Grade Coefficients and the 90-10 Differential

---

### Preliminaries

First, let's navigate to the analysis folder and set up some global macros. For this exercise, it's going to be useful to have macros for the start year, end year, and number of years covered by our sample.

    cd "~/Dropbox/@10,000 feet and Runway/MA in Economics at Hunter/ECO 727 - Data Analysis and Research Methods/Analysis"
    set scheme s2color
    global ex=4
    global lname="Readman"
    global syear=1982 // start year
    global eyear=2024 // end year
    global numyear=$eyear-$syear+1

---

### Part A: Using the Merged CPS-ORG/CPI Data from Exercise 3

Our final output from this exercise is to plot and analyze the trend of return to schooling with the weekly log-wage differential. We're going to study the effects of each year of schooling on wages to merge with our log-differentials for each year in our sample.

Let's start by using the CPS-ORG data including the CPI and real weekly wage observations prepared in exercise 3 and making a few edits. First, we'll drop any observations with missing `earnweek`. Then, we'll create a new variable for `experience` as a function of `age` and `grade`, dropping any observations where this calculation yields non-positive values (experience <= 0). Finally, we'll generate experience-squared (`exp2`) to capture diminishing marginal returns to experience and add the log of real wage `lrwage`.

    use "../Data/CPS-ORG with CPI, 1982-2024.dta", replace
    keep if earnweek!=.
    generate experience=age-grade-6
    drop if experience <=0 // experience less then 0 makes no sense
    label variable experience "experience as function of age and education"
    generate exp2=experience^2
    label variable exp2 "experience squared"
    generate lrwage=log(rwage)
    label variable lrwage "log of real weekly wage"

Let's check our new dataset's structure and descriptive statistics for our key continuous variables:

    describe
    summarize

After dropping observations with missing `earnweek` and non-positive experience values, we now have 6,574,458 observations across 27 variables, three more than the 24 we started with (the new variables being `experience`, `exp2`, and `lrwage`).

The summary statistics show our calculated `experience` variable ranges from 1 to 58 years, with a mean of about 20 years. The standard deviation of 12.1 years indicates substantial variation in work experience across the sample. The quadratic term `exp2` ranges from 1 to 3,364 (58²), which captures the diminishing returns to experience in our wage regressions.

---

### Part B: Estimating a Simple log-Wage Regression

Let's estimate log-wage by grade and experience using appropriate CPS earnings weights.

    regress lrwage grade experience exp2 [pw=earnwt]
     test experience exp2
     display "The log-wage profile peaks at" , %4.1f -e(b)[1,2]/(2*e(b)[1,3]) , "years of experience." // Compute and display peak experience

The regression results show that each year of education (`grade`) is associated with approximately 11.5% increase in wages, holding `experience` constant. The coefficient on `experience` is positive (0.052) while the coefficient on `exp2` is negative (-0.0009), indicating that wages increase with experience but at a diminishing rate. This creates a concave wage profile over a worker's career.

Testing the joint significance of the experience terms yields an F-statistic over 99,999, confirming that experience has a highly significant effect on wages. The wage profile peaks when the first derivative with respect to experience equals zero, which occurs at 29.2 years of experience. After this point, additional years of experience are associated with lower wages, though relatively few workers in our sample have more than 30 years of experience.

---

### Part C: Adding Factor Variables

Now, let's add factor variables using Stata syntax to generate all the dummies for our categorical variables and estimate effects for all categories of `race`, `region`, `occupation`, `industry`, as well as `year` effects.

    regress lrwage grade experience exp2 female i.race i.region i.occupation i.industry i.year [pw=earnwt]

Adding these controls slightly reduces the return to education to about 8.3% per year. The female wage gap remains substantial at approximately -31%, holding education, experience and other factors constant.

Testing the joint significance of each categorical variable:

    testparm i.race
    testparm i.region
    testparm i.occupation
    testparm i.industry
    testparm i.year

All groups of factor variables are highly significant. Among racial groups, Black workers earn about 7.5% less than White workers. Regional variation is substantial, with workers in the East South Central division earning about 11% less than those in New England. Occupation effects show the largest wage differentials, with private household service workers earning roughly 82% less than professionals. Among industries, retail trade workers earn about 29% less than those in agriculture, while public administration workers earn about 13% more.

---

### Part D: Estimating Year-Specific Grade Coefficients

We need to estimate the grade effects for each year, so we'll add an interaction between year and grade, controlling for `experience` and our other factor variables.

    regress lrwage year#c.grade experience exp2 female i.race i.region i.occupation i.industry i.year [pw=earnwt]

Stata stores all the coefficients from this regression in the matrix e(b), from which we can pull all the interaction coefficients into a column vector and then create a new two-column table of `grade` effect for each year. This requires a few steps.

First, let's retrieve all the coefficients of our interaction term `year#c.grade` and store them as a new column vector 'b':

    matrix b = e(b)[1, 1..$numyear]' // store year#c.grade coefficients as column vector 'b'
    clear

Then, using `svmat2`, we can create a new two-column table of the 'name' of each row and the value, i.e. the 'year' and grade effect for that year:

    svmat2 b, names(grade_coef) rnames(tag)
    list, noobs

While the column names and 'year' values are a mess, this has got us the data we need. Let's clean these up and convert our grade effects to percentages as `grade_coef`:

    generate year=real(substr(tag, 1, 4))
    drop tag
    order year grade_coef
    replace grade_coef = grade_coef * 100 // convert to percentages
    format grade_coef %4.1f

We now have a new data set of grade effects for each year in our sample:

    list, noobs
    describe
    summarize

The data reveal a substantial increase in the return to education over 1982-2024. The rate of return to an additional year of schooling rose from about 5.6% in 1982 to peak at 9.3% in 2013, before moderating slightly to 9.0% by 2024. This nearly 4 percentage point increase in the return to education represents a dramatic rise in the value of schooling in the labor market.

Let's save this as a temp file to merge with the log-wage differentials in part E.

    tempfile temp1
    save `temp1', replace

---

### Part E: Merging the Grade Coefficients with the 90-10 Differential (in logs)

Recall our goal here is to plot the rate of return to schooling overlayed on our log-wage differential to see how or if they match. Let's use our log-differential data from Exercise 3 and keep just the `year` and `ldiff` variables.

    use "../Data/CPS-ORG, Wage Percentiles, 1982-2024.dta", clear
    keep year ldiff
    list, noobs
    describe
    summarize

This is exactly what we need and ready to merge in the year-specific grade effects from part D. Let's save it as another temp.

    tempfile temp2
    save `temp2', replace

Now, since the tables we're merging are a one-to-one match on `year`, we'll merge as follows, testing for a full match along the way using `assert`. While we're here, let's format `grade_coef` to one decimal place to make it a little easier on the eye.

    use `temp1', clear
    merge 1:1 year using `temp2'
    assert _merge==3
    drop _merge

Let's check the final table is what we need for our final plots in part F:

    list
    describe
    summarize 

Our merged dataset combines two key measures of labor market inequality: the return to education ranging from 5.6% to 9.3%, and the 90-10 log-wage differential which varies between 173% and 197%. Having these measures in a single dataset will allow us to examine their co-movement over time and explore how changes in the value of education relate to overall wage inequality.

---

### Part F: Plotting the Relationship Between the Grade Coefficients and the 90-10 Differential

Let's visualize how returns to education and wage inequality have evolved together over time.

    line ldiff year, yaxis(1) || line grade_coef year, yaxis(2) ///
     xtitle(Year) xlabel(1980(10)2025) xtick(1980(5)2015) ///
     ytitle("90-10 log-Wage Differential (%)", axis(1)) ylabel(150(10)200, format(%10.0g) noticks axis(1)) ///
     ytitle("Rate of Return to Schooling (%)", axis(2)) ylabel(5(1)10, noticks axis(2)) ///
     text(183 1985 "90-10 Differential") text(155 1985 "Return to Schooling") ///
     scheme(Wide727Scheme) name(g1, replace)

![Earnings inequality and the Return to Schooling](Results/inequality_schooling_line.png)

The striking feature of this graph is how closely the return to schooling tracks overall wage inequality, particularly from the mid-1990s onward. While the relationship is less clear in the 1980s, the parallel movement of these two measures over the past 25+ years tells us something important: education has become a crucial driver of earnings differences in the modern labor market. When the payoff to education rises, we see wider gaps between high and low earners, highlighting just how much schooling makes a difference to income.

    scatter ldiff grade_coef || lfit ldiff grade_coef ||, ///
     xtitle(Rate of Return to Schooling (%)) xlabel(4(1)10) ///
     ytitle("90-10 log-Wage Differential (%)") ylabel(160(10)200, format(%10.0g) noticks) ///
     scheme(Squarish727Scheme) name(g2, replace)

![Earnings inequality and the Return to Schooling](Results/inequality_schooling_scatter.png)

The scatter plot reinforces this story, showing a clear positive relationship between returns to education and wage inequality. 

---