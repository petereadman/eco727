## Exercise 5: Reproducing Angrist and Krueger’s Table 6

Peter Readman

---

### Plan    

---

- Use the Birth Quarter Data, prepared from Angrist and Krueger's paper
- Estimate log-Wage Regressions by OLS and 2SLS
- Reproduce Angrist and Krueger’s Table 6 estimates
- Comparing the Estimates
- Create a Table of Estimates from the log-Wage OLS and 2SLS regressions
- Reproduce Angrist and Krueger’s Table 6

---

### Preliminaries

---

### Part A: Use the “Birth Quarter.dta” Data

Table VI from Angrist and Krueger presents data from the 1940's cohort, so we'll restrict our data accordingly:

    use "../Data/Birth Quarter.dta" if cohort==4, clear

Let's peak at the data. We want to make sure we have 1980 census data and the number of observations matches those reported by A&K.

    describe
    tabulate census
    assert census==80
    assert _N==486926

Confirmed. 

A&K's table VI includes 'Age' and 'Age-squared' which are measured in quarters of years. The Birth Quarter data we're using includes `ageq` (age in quarters of years) and we'll generate the required Age-squared variable from this. We're also going to need a 1st-quarter variable `q1`.

    generate ageqsq=ageq^2
    tab qob
    generate q1=1 if qob==1
    replace  q1=0 if q1==.
    tab q1
    label variable q1 "quarter 1"
    label variable ageqsq "age quarter squared"
    describe

Done. Now we have everything we need to start reproducing the results for the tables.

---

### Part B: Estimate the log-Wage Regression by OLS and 2SLS

    eststo wald:   ivregress 2sls lwklywge (educ = q1)
     estat firststage
    eststo qob_iv: ivregress 2sls lwklywge (educ = ib4.qob) ib49.yob, first
     estat firststage
     estat overid

---

### Part C: Reproduce Angrist and Krueger’s Table 6

eststo column_1: regress lwklywge educ i.yob // OLS (1)
    eststo column_2: ivregress 2sls lwklywge (educ = ib4.qob#ib49.yob) ib49.yob // TSLS (2)
     estat firststage
     estat overid
    eststo column_3: regress lwklywge educ i.yob ageq ageqsq // OLS (3)
    eststo column_4: ivregress 2sls lwklywge (educ = ib4.qob#ib49.yob) ib49.yob ageq ageqsq // TSLS (4)
     estat firststage
     estat overid
    eststo column_5: regress lwklywge educ i.race i.smsa i.married i.yob i.region // OLS (5)
    eststo column_6: ivregress 2sls lwklywge (educ = ib4.qob#ib49.yob) i.race i.smsa i.married ib49.yob ib9.region // TSLS (6)
     estat firststage
     estat overid
    eststo column_7: regress lwklywge educ i.race i.smsa i.married i.yob i.region ageq ageqsq // OLS (7)
     estat firststage
     estat overid
    eststo column_8: ivregress 2sls lwklywge (educ = ib4.qob#ib49.yob) i.race i.smsa i.married ib49.yob ib9.region ageq ageqsq // TSLS (8)
     estat firststage
     estat overid

---

### Part D: Compare the Estimates

#### Wald estimate in Part B compared to the Wald estimates in Angrist and Krueger’s table 3

The estimates for the 1940s cohort differ from Angrist and Krueger’s results. The Wald estimate in Part B, using three birth-quarter instruments, is close to zero, while Angrist and Krueger report estimates between 0.0715 and 0.1020 in Table III. This suggests that the instruments are weaker within the 1940s cohort and capture less variation in schooling.

#### 2SLS estimate with three birth-quarter instruments compared to the 2SLS estimates with birth-quarter × birth-year instruments?

The 2SLS estimates tell a similar story. Using three birth-quarter instruments gives smaller and less precise estimates than models with birth-quarter × year-of-birth interactions. Adding interactions brings the estimates closer to those in Angrist and Krueger, likely because the interactions capture more variation in education.

#### Over-identification 

Over-identification tests show problems with both specifications. The Sargan test rejects the null for models with three birth-quarter instruments and for those with 29 birth-quarter × year-of-birth interactions. This suggests that the instruments may not satisfy the exclusion restriction, even with additional interactions.

---

### Part E: Create a Table of Estimates from Part B

Let's see all the regression results stored so far.

    estimates dir

    esttab ols wald qob_iv, ///
     nodepvars nostar obslast nobaselevels varwidth(24) ///
     title("Least-Squares Estimates of the log-Wage Regression") ///
     mtitles("OLS" "1 IV" "QOB IV") ///
     b(4) se(4) stats(r2 N, fmt(%4.3f %9.0fc) labels("R^2" "N")) ///
     coeflabels(_cons Constant educ "Years of Education") ///
     order(_cons educ) ///
     indicate("Birth Year Effects = *.yob") ///
     nonotes addnotes("Standard errors in parentheses." "Data source: 1980 Census.")

From the above, we can export to a LaTeX file for a more professional presentation.

    esttab ols wald qob_iv using results/Exercise_5_table_a_readman.tex, replace ///
     b(4) se(4) stats(r2 N, fmt(%4.3f %9.0fc) labels("\$R^2\$" "\$N\$")) ///
     nodepvars nostar obslast nobaselevels varwidth(24) ///
     alignment(D{.}{.}{-1}) ///
     title("Least-Squares Estimates of the log-Wage Regression") ///
     mtitles("OLS" "1 IV" "QOB IV") ///
     coeflabels(_cons Constant educ "Years of education") ///
     order(_cons educ) ///
     indicate("Birth Year Effects = *.yob", labels(\checkmark \text{})) ///
     nonotes addnotes("Standard errors in parentheses." "Data source: 1980 Census.")

---

### Part F: Reproduce Angrist and Krueger’s Table 6

    esttab column_1 column_2 column_3 column_4, ///
     order(educ *.yob ageq ageqsq) ///
     b(4) se(4) stats(r2, fmt(%4.3f) labels("R^2")) ///
     nodepvars nostar obslast nobaselevels varwidth(24) ///
     title("OLS and TSLS Estimates of the Return to Education for Men Born 1940-1949: 1980 Census") ///
     indicate("9 Year-of-birth dummies = *.yob") ///
     mtitles("OLS" "2SLS" "OLS" "2SLS") ///
     coeflabels(educ "Years of education" ageq "Age" ageqsq "Age-squared") ///
     nonotes addnotes("a. Standard errors in parentheses. Sample size is 486,926. Instruments are a full set of quarter-of-birth times year-of-birth interactions." ///
      "Sample consists of males born in the United States. The sample is drawn from the 5 percent samples of the 1980 Census." ///
      "The dependent variable is the log of weekly earnings. Age and age-squared are measured in quarters of years." ///
      "Each equation also includes an intercept.")

Again, we can add some code and export this to LaTeX

    esttab column_1 column_2 column_3 column_4 using results/Exercise_5_table_b_readman.tex, replace ///
     order(_cons educ *.yob ageq ageqsq) ///
     b(4) se(4) stats(r2, fmt(%4.3f) labels("\$R^2\$")) ///
     nodepvars nostar obslast nobaselevels varwidth(24) alignment(D{.}{.}{-1}) ///
     title("OLS and TSLS Estimates of the Return to Education for Men Born 1940-1949: 1980 Census") ///
     indicate("9 Year-of-birth dummies = *.yob", labels(\text{yes} \text{no})) ///
     mtitles("OLS" "2SLS" "OLS" "2SLS") ///
     coeflabels(_cons Constant educ "Years of education" ageq "Age" ageqsq "Age-squared") ///
     nonotes addnotes("a. Standard errors in parentheses. Sample size is 486,926. Instruments are a full set of quarter-of-birth times year-of-birth interactions." ///
      "Sample consists of males born in the United States. The sample is drawn from the 5 percent samples of the 1980 Census." ///
      "The dependent variable is the log of weekly earnings. Age and age-squared are measured in quarters of years." ///
      "Each equation also includes an intercept.")

---
