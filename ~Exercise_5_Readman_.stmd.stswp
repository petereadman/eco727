## Exercise 5: Reproducing Angrist and Krueger’s Table 6

Peter Readman

---

### Plan    

---

- Use the Birth Quarter Data, prepared from Angrist and Krueger's paper
- Estimate log-Wage Regressions by OLS and 2SLS
- Reproduce Angrist and Krueger’s Table 6 estimates
- Comparing the Estimates
- Create a Table of Estimates from the log-Wage OLS and 2SLS regressions
- Reproduce Angrist and Krueger’s Table 6

---

### Preliminaries

---

### Part A: Use the “Birth Quarter.dta” Data

Table VI from Angrist and Krueger presents data from the 1940's cohort, so we'll restrict our data accordingly:

    use "../Data/Birth Quarter.dta" if cohort==4, clear

Let's peak at the data. We want to make sure we have 1980 census data and the number of observations matches those reported by A&K.

    describe
    tabulate census
    assert census==80
    assert _N==486926

Confirmed. 

A&K's table VI includes 'Age' and 'Age-squared' which are measured in quarters of years. The Birth Quarter data we're using includes `ageq` (age in quarters of years) and we'll generate the required Age-squared variable from this. We're also going to need a 1st-quarter variable `q1`.

    generate ageqsq=ageq^2
    tab qob

There are 117,720 individuals in the 1940s cohort born in the first quarter of the year. Let's generate the new `q1` variable and make sure the numbers match.

    generate q1=1 if qob==1
    replace  q1=0 if q1==.
    tab q1
    label variable q1 "quarter 1"
    label variable ageqsq "age quarter squared"

Good: there are 117,720 `q1` observations in our data. Let's do one more `describe` command to make sure we have all the variables we need to recreate Angrist and Krueger's results.

    describe

All variables present and correct. Let's move on.

---

### Part B: Estimate the log-Wage Regression by OLS and 2SLS

Let's start by estimating the effect of education on weekly wages with a simple regression of log weekly wages on years of education, storing the results in Stata.

    eststo ols: regress lwklywge educ

For men born in the 1940s, an additional year of education is associated with a 5.54% increase in weekly wages. This estimate is very precise, with a standard error of 0.0003.

Next, we implement the Wald estimator using 2SLS with first quarter of birth as the only instrument.

    eststo wald: ivregress 2sls lwklywge (educ = q1), first
     estat firststage

The Wald estimate shows a negative but statistically insignificant effect of -0.98% (SE=0.0265) on weekly wages. The first stage reveals that being born in the first quarter reduces education by 0.08 years on average (t=-8.27). While the F-statistic of 68.38 is well above conventional weak instrument thresholds, the first stage R-squared of 0.0001 indicates quarter of birth explains very little of the variation in education.

Finally, we estimate the returns to education using all quarter-of-birth dummies as instruments while controlling for year of birth effects.

    eststo qob_iv: ivregress 2sls lwklywge (educ = ib4.qob) ib49.yob, first
     estat firststage
     estat overid

This specification yields a significant negative effect of education on wages of -6.12% (SE=0.0257). The first stage F-statistic of 28.95 lies between the critical values for 20% maximal relative bias (6.46) and 10% maximal size distortion (22.30), suggesting moderate instrument strength. However, the Sargan test strongly rejects the null of valid instruments (p=0.0043), raising concerns about the exclusion restriction.

---

### Part C: Reproduce Angrist and Krueger’s Table 6

In order to recreate Angrist & Krueger's Table VI, I need the results of eight models: four OLS and four two-stage least squares with instruments. I'll run each regression and store the results in Stata. Here's a brief analysis of each model:

#### Column 1 (OLS)

    eststo column_1: regress lwklywge educ i.yob // OLS (1)

Adding year-of-birth controls to the OLS specification yields a slightly higher return to education of 5.73% (SE=0.0003), with birth-year effects showing consistently lower wages for later cohorts relative to those born in 1940.
    
#### Column 2 (TSLS)
    
    eststo column_2: ivregress 2sls lwklywge (educ = ib4.qob#ib49.yob) ib49.yob // TSLS (2)
     estat firststage
     estat overid

Using quarter-of-birth × year-of-birth interactions as instruments yields a return to education of 5.53% (SE=0.0138), similar to OLS but less precise, though the first-stage F-statistic of 7.62 suggests weak instruments.

#### Column 3 (OLS)

    eststo column_3: regress lwklywge educ i.yob ageq ageqsq // OLS (3)

Adding age and age-squared terms to the model leaves the education coefficient virtually unchanged at 5.73% (SE=0.0003), while the quadratic age terms are both significant and suggest a concave age-earnings profile.

#### Column 4 (TSLS)

    eststo column_4: ivregress 2sls lwklywge (educ = ib4.qob#ib49.yob) ib49.yob ageq ageqsq // TSLS (4)
     estat firststage
     estat overid

Adding age controls increases the estimated return to education to 9.48% (SE=0.0223), but the first-stage F-statistic drops to 3.22, indicating severe weak instrument problems and suggesting caution in interpreting these results.

#### Column 5 (OLS)

    eststo column_5: regress lwklywge educ i.race i.smsa i.married i.yob i.region // OLS (5)

Adding race, SMSA, marital status, and region controls reduces the return to education to 5.20% (SE=0.0003), with all demographic controls showing significant effects on wages as expected.

#### Column 6 (TSLS)

    eststo column_6: ivregress 2sls lwklywge (educ = ib4.qob#ib49.yob) i.race i.smsa i.married ib49.yob ib9.region // TSLS (6)
     estat firststage
     estat overid

Column 6 (2SLS): With full demographic controls, the IV estimate falls to 3.93% (SE=0.0145), while the first-stage F-statistic of 6.85 indicates weak instruments and the Sargan test (p<0.001) suggests potential violations of the exclusion restriction.

#### Column 7 (OLS)

    eststo column_7: regress lwklywge educ i.race i.smsa i.married i.yob i.region ageq ageqsq // OLS (7)

Column 7 (OLS): Adding age controls to the demographic specification leaves the education coefficient virtually unchanged at 5.21% (SE=0.0003), while the quadratic age terms remain significant and suggest a concave age-earnings profile.

#### Column 8 (2SLS)

    eststo column_8: ivregress 2sls lwklywge (educ = ib4.qob#ib49.yob) i.race i.smsa i.married ib49.yob ib9.region ageq ageqsq // TSLS (8)
     estat firststage
     estat overid

In this final specification with full demographic and age controls, the IV estimate increases to 7.79% (SE=0.0239); however, the first-stage F-statistic drops to 2.74, indicating severe weak instrument problems that make these estimates unreliable.

---

### Part D: Compare the Estimates

The Wald estimate for men born in the 1940s (-0.98%, SE=0.0265) is notably different from A&K's estimates for both the 1920s cohort (7.15%, SE=0.0219) and 1930s cohort (10.20%, SE=0.0239).

Moving from three birth-quarter instruments to birth-quarter × birth-year interactions leads to more extreme negative estimates (-6.12% vs -0.98%), contrary to A&K's original findings where using more instruments produced more precise, positive returns to education.

The overidentification tests reject the null of valid instruments in both specifications: for the three birth-quarter instruments (p=0.0043) and for the 29 birth-quarter/birth-year instruments (p<0.001), suggesting potential violations of the exclusion restriction.
---

### Part E: Create a Table of Estimates from Part B

The results of the three regressions we ran in part B are stored in Stata's memory, so let's use the `directory` command to see what we have.

    estimates dir

We'll create a table with three columns, one for each model from part B, which were stored as `ols`, `wald`, `qob_iv`, respectively. Using Stata's `esttab` commands, we'll print a formatted set of results to the log to check before exporting to LaTeX.

    esttab ols wald qob_iv, ///
     title("OLS and 2SLS Estimates of the Return to Education for Men Born 1940-1949: 1980 Census") ///
     mtitles("OLS" "1 IV" "QOB IV") ///
     b(4) se(4) stats(r2 N, fmt(%4.3f %9.0fc) labels("R^2" "N")) ///
     coeflabels(_cons Constant educ "Years of education") ///
     order(_cons educ) ///
     indicate("Birth Year Effects = *.yob") ///
     nodepvars nostar obslast nobaselevels varwidth(24) ///
     nonotes addnotes("Standard errors in parentheses." "Data source: ~1980 Census (Angrist and Krueger, 1991).")

From the above, we can export to a LaTeX file for a more professional presentation.

    esttab ols wald qob_iv using results/Exercise_5_table_a_Readman.tex, replace ///
     title("OLS and 2SLS Estimates of the Return to Education for Men Born 1940-1949: 1980 Census") ///
     mtitles("OLS" "1 IV" "QOB IV") ///
     b(4) se(4) stats(r2 N, fmt(%4.3f %9.0fc) labels("\$R^2\$" "\$N\$")) ///
     coeflabels(_cons Constant educ "Years of education") ///
     order(_cons educ) ///
     indicate("Birth Year Effects = *.yob", labels(\checkmark \text{})) ///
     nodepvars nostar obslast nobaselevels varwidth(24) ///
     alignment(D{.}{.}{-1}) ///
     nonotes addnotes("Standard errors in parentheses." "Data source: ~1980 Census (Angrist and Krueger, 1991).")

---

### Part F: Reproduce Angrist and Krueger’s Table 6

Angrist and Krueger's table VI shows restults from four OLS and four TSLS regressions for men born in the 1940s cohort. We'll reporduce the firt four columns in the table, below.

    esttab column_1 column_2 column_3 column_4, ///
     title("OLS and TSLS Estimates of the Return to Education for Men Born 1940-1949: 1980 Census") ///
     mtitles("OLS" "2SLS" "OLS" "2SLS") ///
     b(4) se(4) stats(r2, fmt(%4.3f) labels("R^2")) ///
     coeflabels(_cons Constant educ "Years of education" race "Race (1=white)" smsa "SMSA (1 = center city)" married "Married (1 = married)" ageq "Age" ageqsq "Age-squared") ///
     order(_cons educ race smsa married ageq ageqsq) ///
     indicate("9 Year-of-birth dummies = *.yob") ///
     nodepvars nostar obslast nobaselevels varwidth(24) ///
     nonotes addnotes("a. Standard errors in parentheses. Sample size is 486,926. Instruments are a full set of quarter-of-birth times year-of-birth interactions." ///
      "Sample consists of males born in the United States. The sample is drawn from the 5 percent samples of the 1980 Census." ///
      "The dependent variable is the log of weekly earnings. Age and age-squared are measured in quarters of years." ///
      "Each equation also includes an intercept.")

Again, we can add some code and export this to LaTeX for more profesional appearance.

    esttab column_1 column_2 column_3 column_4 using results/Exercise_5_table_b_Readman.tex, replace ///
     title("OLS and TSLS Estimates of the Return to Education for Men Born 1940-1949: 1980 Census") ///
     mtitles("OLS" "2SLS" "OLS" "2SLS") ///
     b(4) se(4) stats(r2, fmt(%4.3f) labels("\$R^2\$")) ///
     coeflabels(_cons Constant educ "Years of education" race "Race (1=white)" smsa "SMSA (1 = center city)" married "Married (1 = married)" ageq "Age" ageqsq "Age-squared") ///
     order(_cons educ race smsa married ageq ageqsq) ///
     indicate("9 Year-of-birth dummies = *.yob", labels(\text{yes} \text{no})) ///
     alignment(D{.}{.}{-1}) ///
     nodepvars nostar obslast nobaselevels varwidth(24) ///
     nonotes addnotes("a. Standard errors in parentheses. Sample size is 486,926. Instruments are a full set of quarter-of-birth times year-of-birth interactions." ///
      "Sample consists of males born in the United States. The sample is drawn from the 5 percent samples of the 1980 Census." ///
      "The dependent variable is the log of weekly earnings. Age and age-squared are measured in quarters of years." ///
      "Each equation also includes an intercept.")

---
