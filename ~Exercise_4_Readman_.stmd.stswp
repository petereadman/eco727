## Exercise 4: Factor Variables, CPS-ORG/CPI Data, 1982â€“2024

Peter Readman

---

### Plan

---

- Use the Merged CPS-ORG/CPI Data from Exercise 3
- Estimate a Simple log-Wage Regression
- Add Factor Variables
- Estimate Year-Specific Grade Coefficients
- Merge the Grade Coefficients with the 90-10 Differential (in logs)
- Plot the Relationship Between the Grade Coefficients and the 90-10 Differential

---

### Preliminaries

First, let's navigate to the analysis folder and set up some global macros. For this exercise, it's going to be useful to have macros for start year and number of years covered by our sample.

    cd "~/Dropbox/@10,000 feet and Runway/MA in Economics at Hunter/ECO 727 - Data Analysis and Research Methods/Analysis"
    set scheme s2color
    global ex=4
    global lname="Readman"
    global syear=1982 // start year
    global eyear=2024 // end year
    global numyear=$eyear-$syear+1

---

### Part A: Using the Merged CPS-ORG/CPI Data from Exercise 3

Our final output from this exercise is to plot and analyze the trend of return to schooling with the weekly log-wage differential. We're going to the effects of each year of schooling on wages to merge with our log-differentials for each year in our sample.

Let's start by using the CPS-ORG data including the CPI and real weekly wage observations prepared in exercise 3 and making a few edits.  We can get rid of missing `earnweek` observations, create a new variable for `experience` as a function of `age` and `grade`, generate experience-squared (`exp2`) to cover diminishing marginal return to experience and finally add the log of real wage `lwrage`.

    use "../Data/CPS-ORG with CPI, 1982-2024.dta", replace
    keep if earnweek!=.
    generate experience=age-grade-6
    drop if experience <=0 // experience less then 0 makes no sense
    label variable experience "experience as function of age and education"
    generate exp2=experience^2
    label variable exp2 "experience squared"
    generate lrwage=log(rwage)
    label variable lrwage "log of real weekly wage"

Let's peak at our new data and make sure we have what we need and all makes sense.

    describe
    summarize

The new variables are created. After dropping 163,630 missing `earnweek` values, we now have 6,574,458 observations across 27 variables, three more than the 24 we started with, as expected.

---

### Part B: Estimating a Simple log-Wage Regression

Let's estimate log-wage by grade and experience using appropriate CPS earnings weights.

    regress lrwage grade experience exp2 [pw=earnwt]
     test experience exp2
     display "The log-wage profile peaks at" , %4.1f -e(b)[1,2]/(2*e(b)[1,3]) , "years of experience." // Compute and display peak experience

Testing the significance of `experience`, we note the very large F-stat, confirming the return to `experience` is significant. The sign of the coefficient on `exp2` is negative, reflecting diminishing marginal returns to experience. The turning point of the log-wage profile is at 29.2 years of experience.

---

### Part C: Adding Factor Variables

Now, let's add factor variables using Stata syntax to generate all the dummies for our categorical variables and estimate effects for all categories of `race`, `region`, `occupation`, `industry`, as well as `year` effects.

    regress lrwage grade experience exp2 female i.race i.region i.occupation i.industry i.year [pw=earnwt]

Testing the joint significiance of each categorical variable in turn shows us:

    testparm i.race

`race` effects are all jointly statistically significant. 

    testparm i.region

`region` effects are all jointly statistically significant. 

    testparm i.occupation

`occupation` effects are all jointly statistically significant.

    testparm i.industry

`industry` effects are all jointly statistically significant.

    testparm i.year

`year` effects are all jointly statistically significant.

---

### Part D: Estimating Year-Specific Grade Coefficients

We need estimate the grade effects for each year, so we'll add an interaction between year and grade, controlling for `experience` and our other factor variables.

    regress lrwage year#c.grade experience exp2 female i.race i.region i.occupation i.industry i.year [pw=earnwt]

Stata stores all the coefficients from this regression in the matrix e(b), from which we can pull all the interaction coefficients into a column vector and then create a new two-column table of `grade` effect for each year. This requires a few steps.

First, let's retrieve all the coefficients of our interaction term `year#c.grade` and store them as a new column vactor 'b':

    matrix b = e(b)[1, 1..$numyear]' // store year#c.grade coefficients as column vector 'b'
    clear

Then, using `svmat2`, we can create a new two-column table of the 'name' of each row and the value, i.e. the 'year' and grade effect for that year:

    svmat2 b, names(grade_coef) rnames(tag)
    list, noobs

While the column names and 'year' values are a mess, this has got us the data we need. Let's clean these up and convert our grade effects to percentages as `grade_coef`:

    generate year=real(substr(tag, 1, 4))
    drop tag
    order year grade_coef
    replace grade_coef = grade_coef * 100 // convert to percentages

We now have a new data set of grade effects for each year in our sample:

    list, noobs
    describe
    summarize

This looks good. We have 43 observations, one for each year 1982-2024. The our percentage rate of return to schooling ranges from 5.6 to 9.3, which we'll note for preparing the plot in part F.

Let's save this as a temp file to merge with the log-wage differentials in part E.

    tempfile temp1
    save `temp1', replace

---

### Part E: Merging the Grade Coefficients with the 90-10 Differential (in logs)

Recall our goal here is to plot the rate of return to schooling overlayed on our log-wage differential to see how or if they match.  Let's use our log-differential data from Exercise 3 and keep just the `year` and `ldiff` variables.

    use "../Data/CPS-ORG, Wage Percentiles, 1982-2024.dta", clear
    keep year ldiff
    list, noobs
    describe
    summarize

This is exactly what we need and ready to merge in the year-specific grade effects from part D, Let's save it as another temp.

    tempfile temp2
    save `temp2', replace

Now, since the tables we're merging are a one-to-one match on `year`, we'll merge as follows, testing for a full match along the way using `assert`.

    use `temp1', clear
    merge 1:1 year using `temp2'
    assert _merge==3
    drop _merge

Let's check the final table is what we need for our final plots in part F:

    list
    describe
    summarize 

Good. We now have rates of return to schooling and log-wage differentials from 1982-2024, 43 years in total.

---

### Part F: Plotting the Relationship Between the Grade Coefficients and the 90-10 Differential

We're now ready to produce our line and scatter plots. The line plot has a second vertical axis.

    line ldiff year, yaxis(1) || line grade_coef year, yaxis(2) ///
     xtitle(Year) xlabel(1980(10)2025) xtick(1980(5)2015) ///
     ytitle("90-10 log-Wage Differential (%)", axis(1)) ylabel(150(10)200, format(%10.0g) noticks axis(1)) ///
     ytitle("Rate of Return to Schooling (%)", axis(2)) ylabel(5(1)10, noticks axis(2)) ///
     text(182 1985 "90-10 Differential") text(155 1985 "Return to Schooling") ///
     scheme(Wide727Scheme) name(g1, replace)
    graph export results/inequality_schooling_line.png, name (g1) width(600) replace

![Earnings inequality and the Return to Schooling](Results/inequality_schooling_line.png)

The graph shows that wage inequality and the return to education followed similar trends, both rising steadily from 1980 to around 2010. After 2010, the two lines diverge slightly, with wage inequality stabilizing while the return to education declines modestly, highlighting their close but not perfect relationship over time.

Let's also present the relationship in a scatterplot with a regression line.

    scatter ldiff grade_coef || lfit ldiff grade_coef ||, ///
     xtitle(Rate of Return to Schooling (%)) xlabel(4(1)10) ///
     ytitle("90-10 log-Wage Differential (%)") ylabel(160(10)200, format(%10.0g) noticks) ///
     scheme(Squarish727Scheme) name(g2, replace)
    graph export results/inequality_schooling_scatter.png, name (g2)  width(450) replace

![Earnings inequality and the Return to Schooling](Results/inequality_schooling_scatter.png)

The scatter plot shows the strong positive relationship between wage inequality (90-10 log-wage differential) and the return to education, indicating that higher returns to schooling are associated with greater wage inequality.

---