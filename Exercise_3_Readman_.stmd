
## Exercise 4: Factor Variables, CPS-ORG/CPI Data, 1982–2024

Peter Readman

---

### Plan

---

- append Merged Outgoing Rotation Group (morg) earnings data data sets for years 1979, 1980, 1981 and align with IPUMS
- merge monthly CPI data into CPS 1982-2024 cleaned on 1/7/2024, compute real weekly wage
- plot annual statistics for real weekly wage and difference in 10th/90th percentiles

---

### Preliminaries

As usual, we start by moving to the analysis folder and setting up some macros

    cd "~/Dropbox/@10,000 feet and Runway/MA in Economics at Hunter/ECO 727 - Data Analysis and Research Methods/Analysis"
    global ex=3
    global lname="Readman"
    global eyear=2024
    global emonth=11

### Appending and Cleaning CPS-ORG Data

We want to combine data from three years' worth of Merged Outgoing Rotation Group (morg) data into one data set. Since each morg data set has the same variables, we'll use `append`. We don't need all the variables, so we'll keep what we need.

    clear
    append using "../Data/morg79.dta" "../Data/morg80.dta" "../Data/morg81.dta"
    keep year intmonth minsamp classer esr age race sex gradeat uhours earnwke earnwt
    
Reviewing the summary statistics of the cleaned CPS-ORG dataset confirm the appended data only have the variables we need for all observations:

    describe

The appended data sets have 1,078,331 observations over 12 variables. 

    summarize

It looks like there are missing observations in `classer`, `uhours`, and `earnwke` variables. The good news is these data already conform to only outgoing rotation groups 4 and 8 to match our CPS extract. The year ranges matches what we downloaded, which is always good to see. 

We may want to merge some or all this data into our IPUMS data set sometime, so let's edit the variable and label names to align:

    rename (minsamp intmonth) (mish month)
    label define MONTH 1 "january" 2 "february" 3 "march" 4 "april" 5 "may" 6 "june" ///
                       7 "july" 8 "august" 9 "september" 10 "october" 11 "november" 12 "december"
    label values month MONTH
    label define MISH 4 "four" 8 "eight"
    label values mish MISH
    tabulate month mish

For this analysis, we're only interested in people aged 18–64 who were employed last week, working for a private firm or a governemnt body. Let's filter for these conditions and check some summary statistics.

    numlabel classer, add
    tabulate classer
    keep if inrange(age, 18, 64) & (classer == 1 | classer == 2)

    describe
    summarize
    summarize earnwke, detail
    tab year if earnwke == .

The mean meekly earnings is $268.62 with the median slightly lower at $233. This is interesting as I'd expect the median to be higher than the mean for earnings data, but could be easily explained by considering topcoding. Weekly earnings range from 0 to 999, which looks like a missing value code or topcode. We'll note this and move on.
  
---

### Preparing the CPI Data

We want to analyze how real weekly wages have changed over time. We'll need to adjust our CPS `earnweek` data for inflation using Consumer Price Index (CPI) data from the Bureau of Labor Statistics. We'll need to get the CPI data and merge it into our cleaned CPS data.

    import excel using "../Data/SeriesReport-20250109151624_b18e88.xlsx", cellrange(A12:M124) firstrow clear
    describe
    summarize

The CPI data are for each motnh from 1913–November 2024. That is, 112 observations accross 13 variables: one for `Year` and one for each month `Jan-Dec`. In order to merge these into our CPS data, we'll need to reshape it first to give us a `year` column a `month` column and teh `cpi` for each month of each year.

    rename Year year // rename to match CPS
    rename (Jan-Dec) m#, addnumber
    reshape long m, i(year) j(month)
    rename m cpi
    describe

We should revise the labeling and formatting for ease of use:

    label variable month "Month"
    label variable cpi "Consumer Price Index"
    format cpi %9.3f

Sorting the CPI data by year and month will complete the preparation for merging into our CPS data. A peak at the first couple years' CPI data confirms it's in the correct format.

    sort year month
    list if year>=1982 & year<1984, noobs clean

After saving, we have the following summary statistics and variables:
    summarize
    describe

---

### Merging CPI and CPS-ORG Data

Let's use our cleaned CPS 1982-2024 data from the previous session. This is our master data set into which we'll merge the CPI data we just prepared. Let's sort it chronologically so it aligns with the CPI data.

    use "../Data/CPS ORGs, 1982-2024, Cleaned.dta", clear
    describe
    summarize
    sort year month

The data are as expected, with 6,738,088 observations over 22 variables, including weekly earnings (`earnweek`), prepared with all topcoding and missing values accounted for. We also have the earnings weight `earnwt`, which we'll need to calculate some real wage statistics later.

Now let's merge the CPI data into the CPS, adding a check via `assert` to make sure we're only keeping any matched values. We don't need CPI data for any years outside of our CPS range 1982-2024, after all.

    merge m:1 year month using "../Data/CPI 1982-2024.dta", keep(match)
    assert _merge ==3
    drop _merge //

Assert didn't fail, confirming our data now has CPI data merged into it, keeping only matching values. Stata's `_merge` variable can be dropped and we move on.
    
Recall that our goal is to analyze real weekly wage changes since 1982, so let's now use the merged CPI values to compute a new real wage (`rwage`) variable.

    summarize cpi if year==$eyear & month==$emonth, meanonly
    return list
    scalar cpi_scalar = r(mean)
    generate rwage=(cpi_scalar*earnweek)/cpi // latest month dollars
    label variable rwage "real wage in November 2024 dollars"

Let's check to make sure `rwage` appears in our variables and peak at the summary statistics.

    describe

    summarize

We have the same number of missing real wage observations as we do for the original weekly wages (`earnweek`), which makes sense. The mean real weekly wage is $1187.70 with a range of 1 cent to nearly $18k.
We'll save that data for further analysis and conclude by plotting some annual statistics of real weekly wages over time.
---

### Annual Summary Statistics and Visualization

Let's calculate the mean, median and 10th and 90th percentiles from 1982 to end of 1994. From these, we can calculate the difference between the logs of the 10th and 90th percentiles to plot income inequality over time.

    collapse (mean) mean=rwage /// mean
                  (p10) p10=rwage /// 10th percentile
                  (p50) p50=rwage /// 10th percentile
                  (p90) p90=rwage /// 90th percentile
                  [pw=earnwt], by(year)
    generate ldiff=100*(ln(p90) - ln(p10))
    format ldiff %4.1f
    
    describe
    summarize
    list in 1/5, noobs

Figure 1 shows trends in wage percentiles (10th, 50th, 90th, and mean) from 1982 to 2024, highlighting disparities in wage growth across income levels. The widening gap between the 90th and 10th percentiles indicates increasing wage inequality over the period.

![Real Weekly Wage Percentiles, 1982–2924](Results/fig1.png)

Figure 2 illustrates the log differences between the 90th and 10th percentiles of real weekly wages from 1982 to 2024. It captures changes in wage inequality over time, showing fluctuations followed by a steep incline, peaking around 2010–2015. Since then, there's a stinking decline in real wage inequality.

![Income Inequality, 1982–2924](Results/fig2.png)

---
