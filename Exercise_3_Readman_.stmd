
## Exercise 3: Organizing the CPI and CPS-ORG Data

Peter Readman

---

### Plan

---

- append morg data sets for years 1979, 1980, 1981 and align with IPUMS
- merge monthly CPI data into CPS 1982-2024 cleaned on 1/7/2024, compute real weekly wage
- Plot annual statistics for real weekly wage and difference in 10th/90th percentiles

---

### Preliminaries

As usual, we start by moving to the analysis folder and setting up some macros

    cd "~/Dropbox/@10,000 feet and Runway/MA in Economics at Hunter/ECO 727 - Data Analysis and Research Methods/Analysis"
    global ex=3
    global lname="Readman"
    global eyear=2024

### Appending and Cleaning CPS-ORG Data

Three data sets of morg data were downloaded from ____ and appended, keeping a subset of variables:

    clear
    append using "../Data/morg79.dta" "../Data/morg80.dta" "../Data/morg81.dta"
    keep year intmonth minsamp classer esr age race sex gradeat uhours earnwke earnwt
    
Summary statistics of the cleaned CPS-ORG dataset confirm the presence of key variables for analysis:

    describe
    summarize
    list in -10/l, noobs

We may want to merge some or all this data into our IPUMS data set, so let's edit the variable and label names to align:

    rename (minsamp intmonth) (mish month)
    label define MONTH 1 "january" 2 "february" 3 "march" 4 "april" 5 "may" 6 "june" ///
                       7 "july" 8 "august" 9 "september" 10 "october" 11 "november" 12 "december"
    label values month MONTH
    label define MISH 4 "four" 8 "eight"
    label values mish MISH
    tabulate month mish

For this analysis, we're only interested in people aged 18â€“64 who were employed last week, working for a private firm or a governemnt body.

    numlabel classer, add
    tabulate classer
    keep if inrange(age, 18, 64) & (classer == 1 | classer == 2)

    describe
    summarize
    summarize earnwke, detail
    tab year if earnwke == .

The mean meekly earnings is $268.62 with the median slightly lower at $233. Weekly earnings range from 0 to 999, which looks like a missing value code or topcode. We'll note this and move on.
  
---

### Preparing the CPI Data

The CPI data were downloaded from the Bureau of Labor Statistics and imported.

    import excel using "../Data/SeriesReport-20250109151624_b18e88.xlsx", cellrange(A12:M124) firstrow clear
    describe
    summarize

Let's reshape ...

    rename Year year // rename to match CPS
    rename (Jan-Dec) m#, addnumber
    reshape long m, i(year) j(month)
    rename m cpi
    describe

We should revise the labeling and formatting for ease of use

    label variable month "Month"
    label variable cpi "Consumer Price Index"
    format cpi %9.3f

Sorting the CPI data by year and month will ready the data for merging into our CPS data. A quick of the first couple years' CPI data confirms it's in the correct format.

    sort year month
    list if year>=1982 & year<1984, noobs clean

After saving, we have the following summary statistics and variables

    summarize
    describe

---

### Merging CPI and CPS-ORG Data

To integrate the two datasets, the CPI data were merged into the CPS-ORG dataset by year and month. A new variable for real weekly wages was calculated by dividing nominal earnings by the CPI. The resulting dataset was saved for further analysis.

    use "../Data/CPS ORGs, 1982-2024, Cleaned.dta", clear
    describe
    summarize
    sort year month

Now it's ready to merge in CPI into the CPS

    merge m:1 year month using "../Data/CPI 1982-2024.dta", keep(match)
    assert _merge ==3
    drop _merge // do we drop _merge if it works?
    
We want to analyze the real wage, adjusted for inflation

    summarize cpi if year==2024 & month==11, meanonly
    return list
    scalar cpi_nov24 = r(mean)
    generate rwage=cpi_nov24*earnweek/cpi // November 2024 dollars
    label variable rwage "real wage"

Summary statistics of the merged data show the inclusion of the CPI and real weekly wages:

    describe
    summarize

---

### Annual Summary Statistics and Visualization

The data were collapsed to annual statistics, including the mean, 10th, 50th, and 90th percentiles of real weekly wages. These metrics were plotted to illustrate wage trends over time.

    collapse (mean) mean=rwage /// mean
                  (p10) p10=rwage /// 10th percentile
                  (p50) p50=rwage /// 10th percentile
                  (p90) p90=rwage /// 90th percentile
                  ,by(year)
    generate ldiff=ln(p90)-ln(p10)  

    describe
    summarize
    list in 1/5, noobs

The figure below displays the trends in real weekly wages over the analyzed period:

![Real Wage Percentiles, 1982â€“2924](Results/fig1.png)

---
